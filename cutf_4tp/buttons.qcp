// button and multiple button



void() button_wait;
void() button_return;

void() button_wait =
{
	self.state = 0;
	self.nextthink = self.ltime + self.wait;
	self.think = button_return;
	activator = self.enemy;
	SUB_UseTargets();
	self.frame = 1;			// use alternate textures
};

void() button_done =
{
	self.state = 1;
};

void() button_return =
{
	self.goal_state = 2;

	self.state = 3;
	SUB_CalcMove (self.pos1, self.speed, button_done);
	self.frame = 0;			// use normal textures
	if (self.health)
		self.takedamage = 1;	// can be shot again
};


void() button_blocked =
{	// do nothing, just don't come all the way back out
};


void() button_fire =
{
	if (self.state == 2 || self.state == 0)
		return;

	sound (self, 2, self.noise, 1, 1);

	self.state = 2;
	SUB_CalcMove (self.pos2, self.speed, button_wait);
};


void() button_use =
{
	self.enemy = activator;
	button_fire ();
};

/*void() button_touch =
{
	local entity te;

	if (other.classname != "player")
		return;

	if (self.goal_activation & #TFGA_SPANNER)
		return;

	if (!Activated(self,other))
	{
		// If an else goal should be activated, activate it
		if (self.else_goal != 0)
		{
			te = Findgoal(self.else_goal);
			if (te)
				DoResults(te, other, (self.goal_result & #TFGR_ADD_BONUSES));
		}

		return;
	}

	self.enemy = other;
	button_fire ();
};*/

void() button_touch =
{
	local entity te;

	if (other.classname != "player")
		return;

    // OfN --------------------------------------//
    //- BUGFIX Avoid observers touching buttons  //
	if (other.playerclass == 0)
		return;
    if (other.done_custom & 4) // skip ppl customizing
        return;
    //------------------------------------------//

	if (self.goal_activation & 8)
		return;

	if (!Activated(self,other))
	{
		// If an else goal should be activated, activate it
		if (self.else_goal != 0)
		{
			te = Findgoal(self.else_goal);
			if (te)
				DoResults(te, other, (self.goal_result & 2));
		}

		return;
	}

	self.enemy = other;
	button_fire ();
};


void() button_killed =
{
	if (self.goal_activation & 8)
		return;

	self.enemy = damage_attacker;
	self.health = self.max_health;
	self.takedamage = 0;	// wil be reset upon return
	button_fire ();
};


/*QUAKED func_button (0 .5 .8) ?
When a button is touched, it moves some distance in the direction of it's angle, triggers all of it's targets, waits some time, then returns to it's original position where it can be triggered again.

"angle"		determines the opening direction
"target"	all entities with a matching targetname will be used
"speed"		override the default 40 speed
"wait"		override the default 1 second wait (-1 = never return)
"lip"		override the default 4 pixel lip remaining at end of move
"health"	if set, the button must be killed instead of touched
"sounds"
0) steam metal
1) wooden clunk
2) metallic click
3) in-out

TF Note:
If the spawnflags has the #BUTTON_START_OUT (32), then the button
will automatically fire at the start of the level, allowing you
to light in the out position.

*/
void() func_button =
{
	//local float		gtemp, ftemp;

	if (CheckExistence() == 0)
	{
		dremove(self);
		return;
	}

	if (self.sounds == 0)
	{
		precache_sound ("buttons/airbut1.wav");
		self.noise = "buttons/airbut1.wav";
	}
	if (self.sounds == 1)
	{
		precache_sound ("buttons/switch21.wav");
		self.noise = "buttons/switch21.wav";
	}
	if (self.sounds == 2)
	{
		precache_sound ("buttons/switch02.wav");
		self.noise = "buttons/switch02.wav";
	}
	if (self.sounds == 3)
	{
		precache_sound ("buttons/switch04.wav");
		self.noise = "buttons/switch04.wav";
	}
	
	SetMovedir ();

	self.movetype = 7;
	self.solid = 4;
	setmodel (self, self.model);

	self.blocked = button_blocked;
	self.use = button_use;

	if (self.health)
	{
		self.max_health = self.health;
		self.th_die = button_killed;
		self.takedamage = 1;
	}
	else
		self.touch = button_touch;

	if (!self.speed)
		self.speed = 40;
	if (!self.wait)
		self.wait = 1;
	if (!self.lip)
		self.lip = 4;

	self.state = 1;

	self.pos1 = self.origin;
	self.pos2 = self.pos1 + self.movedir*(fabs(self.movedir*self.size) - self.lip);

	if (self.spawnflags & 32)
		button_fire ();
};



