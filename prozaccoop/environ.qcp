/*=======================================================//
// environ.QC - CustomTF 3.2.OfN           - 05/8/2004 - //
// by Sergio FumaÒa Grunwaldt - OfteN [cp]               //
=========================================================//
 World environment (special FX's & daylight/brightness) 
---------------------------------------------------------//
 Brightness/daylight fade in/out's and other environment
 stuff is handled here - CREDITS: Thanks Grievre!
 Aren't the storm and earthquake awesome?! =)
_________________________________________________________//

 *** WARNINGs about Storm and Earthquake use: ***

 I don't recommend using earthquake or storm on net 
 servers as they may cause lag/FPS drops, for LAN they are
 great, no problems.
 
 The drawback of the storm is that on some 
 computer/client combos FPS may get decreased by the
 continuous brightness changes. (observed on a P3+Voodoo5
 with gl-fuhquake as client mainly specially on big maps).
 With software clients and original ID opengl client
 there should be no problem, but experiment yourself.

 These warnings doesn't apply to DayTime "emulation", as
 its brightness changes are done at a maximum rate of
 1 per-second, and it wouldn't cause much trouble if any.

 But remember, do your own tests and see if people playing
 experience any lag/FPS drops, in some circumstances like
 a server with only a few players or every player with
 good computer and connection it would even not be
 noticed at all! Enjoy!

=========================================================*/

void() RemoveFadeTimer;
string(float brightness) ReturnBrightness;
void(float brightness) SetWorldBrightness;
entity(float brightness, float fadetime, float delaytime, float factor) FadeWorld;
void() EnvironmentPrecachees;
void(float oldstorm, float oldearthquake, float olddaytime, float oldrunes) UpdateEnvironment;
void() GetEnvironmentGlobals;
void() ApplyMapFlags;

void() InitDayTime;
void() InitStorm;
void() InitEarthquake;
void(float report) CleanUpDayTime;
void(float report) CleanUpStorm;
void(float report) CleanUpEarthquake;

void(float thundernum, float vol) ThunderSound;
void(entity tstorm, float thunderdelay) StormLightning;
float(entity tstorm) GetThunderDelay;
entity(entity tstorm) StormSelectMachine;
entity(entity tstorm) StormSelectPlayer;

// External
void(vector org, float damage) SpawnBlood;
float(entity tester) IsBuilding;
void() GetRunesGlobal;
void(float oldrunes) UpdateRuneStuff;

//validate files
float(string strr1) validatelit;
float(string strr2) validatesky;
float(string strr3) validatehrt;


/*===============================================================================================

EXPLANATION OF HOW THE ENTITY FIELDS ARE USED (thnx? np.. :P)
---------------------------------------------

For world fade in/out entity:
-----------------------------

.has_tesla      - Desired final brightness
.has_sentry     - Original brightness when fading started
.heat           - Frame counter
.has_sensor     - Number of steps calculated for the fading
.has_teleporter - Determines special action on fading end: 0 = nothing, 1 = set special light on 2 = set it off 3 = adjust daylight var along with fade

For storm entity:
-----------------

.has_tesla   - Distance from battlefield of the storm (it emulates a real thing!)
.has_sentry  - Number of lightning storm events running
.has_holo    - Last thunder sound played (will not play again till another sound is played first)
.has_sensor  - 2nd last thunder sound played (50% chances will not play consecutively)

For storm event entity:
-----------------------

.heat           - Counter for lightning animation
.has_camera     - If TRUE lightning flash is finished for this
.has_teleporter - This is set to TRUE if thunder sound is already played
.has_fieldgen   - Type of lightning thunder to perform (random)
.demon_one      - Points to our main storm entity
.penance_time   - Time at which this effect was started
.job_finished   - Delay that should occur between lightning and thunder sound

For earthquake entity:
----------------------

.has_holo     - It's TRUE if an earthquake is currently happenning
.heat         - Used as a counter for eathquakes
.penance_time - Indicates time at which current earthquake was started
.job_finished - Duration of the current earthquake

For daytime entity:
-------------------

.ltime       - Time at which daytime started
.count       - Total number of days that will occur
.heat        - Size in time of a day
.has_tesla   - Flag that indicates if we are towards midnight (-1) or noon (1)

================================================================================================*/

// Environment settings

// Brightness settings





// Storm settings














// Earthquake settings








// Daytime settings 




//==================================================================================
// Called once-per-map on worldspawn to initialize all the environment stuff

void() InitEnvironment =
{
    // Precache the sounds
    EnvironmentPrecachees();
    
    current_brightness = -1; // To make sure its able to be updated by SetWorldBrightness, see below

    // First, apply flags set on the map for environment
    ApplyMapFlags();

    // Then get the global environment vars (infokeys) which may override map flags
    GetEnvironmentGlobals();
    
    // Set initial world brightness 
    if (no_bright)
        lightstyle(0,"m");
    else
        SetWorldBrightness(5); //(dark because no clients yet)

    // Impide brightness to be altered as we start dark
    specialstate = 1;
    
    // Initialize our environment sub-sections as needed
    UpdateEnvironment(0,0,0,0);
};

//=================================================================
// These return TRUE if server has the file for current map

float(string strr1) validatelit =
{
//    local string st;

    strr1 = strcat("maps/",strr1);
    strr1 = strcat(strr1,".lit");

    return validatefile(strr1);
};

float(string strr2) validatesky =
{
//    local string st;

    strr2 = strcat("maps/",strr2);
    strr2 = strcat(strr2,".sky");

    return validatefile(strr2);
};

float(string strr3) validatehrt =
{
//    local string st;

    strr3 = strcat("maps/",strr3);
    strr3 = strcat(strr3,".hrt");

    return validatefile(strr3);
};

//=======================================================================
// Gets and applies any environment flags that may be on the map

void() ApplyMapFlags =
{
    // Turn on watervis if this map is flagged as watervised and server has autoset_watervis set
    if (infokey(world,"autoset_watervis") == "1") // autoset_watervis is enabled?
    {
        if (world.has_holo & 1)
        {
            bprint(2, "Map Infoç This map supports transparent liquids.\n");

            if (cvar("watervis")!= 1)
            {
                RPrint("Turning ON watervis... (map flagged as watervised)\n");
                //localcmd("watervis 1\n"); 
                cvar_set("watervis","1");
            }
        }
        else
        {
            if (cvar("watervis")!= 0)
            {
                RPrint("Turning OFF watervis... (map not flagged as watervised)\n");
                //localcmd("watervis 0\n");
                cvar_set("watervis","0");
            }
        }
    }

    // Inform clients about any existing special extra files for this map...
    dprint("Looking for any map extra files...\n");
    // Does the server have a coloured lighting file for this map?

    if (validatelit(mapname))
        bprint(2,"Map Infoç Coloured lighting for this map is avaliable. Type ¢cmd special getlit¢ to download it.\n");
    // Does the server have a paked skybox file for this map?
    if (validatesky(mapname))
        bprint(2,"Map Infoç Packed skybox for this map is avaliable. Type ¢cmd special getsky¢ to download it.\n");
    // Does the server have a hi-res textures file for this map? 
    if (validatehrt(mapname))
        bprint(2,"Map Infoç Hi-Res texture file for this map is avaliable. Type ¢cmd special gethrt¢ to download it.\n");

    // Get standard environment flags "hard-coded" into the map file (worldspawn field)
    if (world.has_holo & 16)
        storm = 1;
    if (world.has_holo & 32)
        earthquake = 1;
    if (world.has_holo & 64)
        daytime = 10;
    if (world.has_holo & 128)
        runes = 1;

    // Disable custom/stock classes if needed
    if (world.has_holo & 256)
        custom_mode = 2;
    if (world.has_holo & 512)
        stock_mode = 2;
    
    // Make sure 1 of them is enabled at least
    if (custom_mode == 2 && stock_mode == 2)
        custom_mode = 0;

    // Apply hard-coded custom money
    if (world.money != 0)
        custom_money = world.money;
    
    // Get the teammate's masks
    friends1_mask = stof(world.noise1);
    friends2_mask = stof(world.noise2);
    friends3_mask = stof(world.noise3);
    friends4_mask = stof(world.noise4);

    // Get disabled/free stuff masks, if map stuff not disabled
    if (infokey(world,"no_mapstuff") != "1")
    {
        disabledstuff1 = world.display_item_status1;
        disabledstuff2 = world.display_item_status2;
        disabledstuff3 = world.display_item_status3;
        disabledstuff4 = world.display_item_status4;
        disabledstuff5 = world.restore_group_no;

        givenstuff1 = world.if_goal_is_active;
        givenstuff2 = world.if_goal_is_inactive;
        givenstuff3 = world.if_goal_is_removed;
        givenstuff4 = world.if_group_is_active;
        givenstuff5 = world.if_group_is_inactive;
    }
};

//===================================================================
// Gets all the environment globals from the localinfo's

void() GetEnvironmentGlobals =
{
    local string st;

    // Get custom daylight setting, if any
    st = infokey(world,"daylight");

    if (st == "") // we should use a default then..
        daylight = 12;
    else
    {
        daylight = stof(st); // we use the custom setting..

        if (daylight < 0 || daylight > 25) // but only if a valid value
            daylight = 12;
    }
    
    // Get the storm setting, if any
    st = infokey(world,"storm");

    if (st != "") 
    {
        storm = stof(st); // we use the custom setting..

        if (storm < 0 || storm > 1) // but only if a valid value
            storm = 0;
    }

    // Get the earthquake setting, if any
    st = infokey(world,"earthquake");

    if (st != "")
    {
        earthquake = stof(st); // we use the custom setting..

        if (earthquake < 0 || earthquake > 1) // but only if a valid value
            earthquake = 0;
    }

    // Get the daytime setting, if any
    st = infokey(world,"daytime");

    if (st != "")
    {
        daytime = stof(st); // we use the custom setting..

        if (daytime < 0) // but only if a valid value
            daytime = 0;
        else if (daytime > 100)
            daytime = 100;
    }

    // Get custom noon/midnight brightness settings, if any
    st = infokey(world,"noon_light");

    if (st == "") // we should use a default then..
        noon_light = 19;
    else
    {
        noon_light = stof(st); // we use the custom setting..

        if (noon_light < 0 || noon_light > 25) // but only if a valid value
            noon_light = 19;
    }

    st = infokey(world,"midnight_light");

    if (st == "") // we should use a default then..
        midnight_light = 5;
    else
    {
        midnight_light = stof(st); // we use the custom setting..

        if (midnight_light < 0 || midnight_light > 25) // but only if a valid value
            midnight_light = 5;

        // If midnight < noonlight just make them the same
        if (midnight_light > noon_light)
            midnight_light = noon_light;
    }

    st = infokey(world,"no_bright");

    if (st == "") // we should use a default then..
        no_bright = 0;
    else
    {
        if (st == "1")
            no_bright = 1;
        else
            no_bright = 0;
    }    

    // Get runes global
    GetRunesGlobal();

    // Get the disabled/free stuff masks settings on server, if any
    st = infokey(world,"dstuff1");
    if (st != "") 
        disabledstuff1 = stof(st); 
    st = infokey(world,"dstuff2");
    if (st != "") 
        disabledstuff2 = stof(st);
    st = infokey(world,"dstuff3");
    if (st != "") 
        disabledstuff3 = stof(st);
    st = infokey(world,"dstuff4");
    if (st != "") 
        disabledstuff4 = stof(st);
    st = infokey(world,"dstuff5");
    if (st != "") 
        disabledstuff5 = stof(st);

    st = infokey(world,"gstuff1");
    if (st != "") 
        givenstuff1 = stof(st); 
    st = infokey(world,"gstuff2");
    if (st != "")
        givenstuff2 = stof(st);
    st = infokey(world,"gstuff3");
    if (st != "") 
        givenstuff3 = stof(st);
    st = infokey(world,"gstuff4");
    if (st != "") 
        givenstuff4 = stof(st);
    st = infokey(world,"gstuff5");
    if (st != "") 
        givenstuff5 = stof(st);
};

//===============================================================
// Sounds for the environment stuff

void() EnvironmentPrecachees =
{
    // Storm thunder sounds
    precache_sound("ambience/thunder1.wav");
    precache_sound("environ/thunder2.wav");
    precache_sound("environ/thunder3.wav");
    precache_sound("environ/thunder4.wav");
    precache_sound("environ/thundrk.wav");

    // Earthquake sounds
    precache_sound("environ/rumble.wav");
    precache_sound("environ/rumblend.wav");
};

//====================================================================================
// Called when environment needs an update (admins change enviro variables etc..)

void(float oldstorm, float oldearthquake, float olddaytime, float oldrunes) UpdateEnvironment =
{
    // Initialize needed environment stuff
    if (oldstorm == 0 && storm != 0)
        InitStorm();
    if (oldearthquake == 0 && earthquake != 0)
        InitEarthquake();
    if (olddaytime == 0 && daytime != 0)
        InitDayTime();
    
    // Shutdown environment stuff if needed
    if (oldstorm != 0 && storm == 0)
        CleanUpStorm(1);
    if (oldearthquake != 0 && earthquake == 0)
        CleanUpEarthquake(1);
    if (olddaytime != 0 && daytime == 0)
        CleanUpDayTime(1);

    // Handled on runes.qc
    UpdateRuneStuff(oldrunes);
};

//==============================================================
// Functions to trigger level fade in and out's

void(float delaytime, float fadetime, float factor) World_FadeIn =
{
    local entity fadetimer;

    specialstate = 1;
    
    fadetimer = FadeWorld(daylight, fadetime, delaytime, factor);

    if (fadetimer)
        fadetimer.has_teleporter = 2;
    else
        specialstate = 0;
};

void(float delaytime, float fadetime, float factor) World_FadeOut =
{
    specialstate = 1;

    FadeWorld(5, fadetime, delaytime, factor);    
};

// Used when observers only in-game brightness and for ceasefire
void(float delaytime, float fadetime, float factor) World_FadeMid =
{
    specialstate = 1;
    
    FadeWorld(8, fadetime, delaytime, factor);
};

// Fade used when intermission runs
void(float delaytime, float fadetime, float factor) World_FadeEnd =
{
    local entity fadetimer;

    fadetimer = FadeWorld(6, fadetime, delaytime, factor);

    if (fadetimer)
        fadetimer.has_teleporter = 3; // Adjust daylight along with fade out
};

//======================================================================================
// Returns the string to be used on a lightstyle call for the given bright intensity

string(float brightness) ReturnBrightness =
{
    brightness = rint(brightness);
    
    if (brightness > 25)
        return "z";

    if (brightness == 0)
        return "a";
    if (brightness == 1)
        return "b";
    if (brightness == 2)
        return "c";
    if (brightness == 3)
        return "d";
    if (brightness == 4)
        return "e";
    if (brightness == 5)
        return "f";
    if (brightness == 6)
        return "g";
    if (brightness == 7)
        return "h";
    if (brightness == 8)
        return "i";
    if (brightness == 9)
        return "j";
    if (brightness == 10)
        return "k";
    if (brightness == 11)
        return "l";
    if (brightness == 12)
        return "m";
    if (brightness == 13)
        return "n";
    if (brightness == 14)
        return "o";
    if (brightness == 15)
        return "p";
    if (brightness == 16)
        return "q";
    if (brightness == 17)
        return "r";
    if (brightness == 18)
        return "s";
    if (brightness == 19)
        return "t";
    if (brightness == 20)
        return "u";
    if (brightness == 21)
        return "v";
    if (brightness == 22)
        return "w";
    if (brightness == 23)
        return "x";
    if (brightness == 24)
        return "y";
    if (brightness == 25)
        return "z";    

    if (brightness < 0)
        return "a";
    
    return "m";
};

//==============================================================================================
// Global function to set overall brightness
// Must be called always, and not directly call lightstyle() ever!

void(float brightness) SetWorldBrightness =
{
    if (no_bright) return;
    
    local string st;
    
    brightness = rint(brightness);
    
    if (brightness == current_brightness) return;

    st = ReturnBrightness(brightness);

    lightstyle(0,st);

    current_brightness = brightness;
};

void() FadeWorld_think;

//========================================================================
// Fades the world brightness to the specified one

entity(float brightness, float fadetime, float delaytime, float factor) FadeWorld =
{
    if (factor == 0)
        factor = 1;

    local entity fadetimer;

    RemoveFadeTimer();

    if (current_brightness == brightness) return world;
    
    fadetimer = spawn();
    fadetimer.classname = "fade";
    fadetimer.think = FadeWorld_think;
    fadetimer.nextthink = time + delaytime;    
    fadetimer.heat = 0;    

    fadetimer.has_sentry = current_brightness;
    fadetimer.has_tesla = brightness;
    
    fadetimer.has_sensor = current_brightness - brightness; // steps

    if (fadetime == 0) // use default rate
        fadetimer.has_camera = fabs(fadetimer.has_sensor) * factor;
    else
        fadetimer.has_camera = fadetime * 10 * factor; // total steps

    fadetimer.has_teleporter = 0; // do nothing when fading ends, default

    return fadetimer;
};

void() FadeWorld_think =
{
    local float bright, current_fraction, current_step;
    
    current_fraction = self.heat / self.has_camera;
    current_step = current_fraction * self.has_sensor; 
    bright = self.has_sentry - current_step;
    
    SetWorldBrightness(bright);

    if (self.has_teleporter == 3)
        daylight = bright;

    self.heat = self.heat + 1;    
    
    if (current_brightness == self.has_tesla)
    {
        if (self.has_teleporter == 1)
            specialstate = 1;
        else if (self.has_teleporter == 2)
            specialstate = 0;
       
        dremove(self);
    }
    else
        self.nextthink = time + 0.1;
};

//=========================================================================
// removes current fade timer

void() RemoveFadeTimer =
{
    local entity te;

    te = find(world,classname,"fade");

    if (te) dremove(te);
};

//-------------------------//
// * Storm *               //
// Thunders and lightning  //
//-------------------------//

void() Storm_think;

//========================================================================
// Initializes storm stuff and starts it

void() InitStorm =
{    
    local entity tstorm;

    CleanUpStorm(0);

    if (!storm) return;

    // Create storm entity
    tstorm = spawn();
    tstorm.classname = "storm";
    tstorm.think = Storm_think;
    tstorm.nextthink = time + 1;

    // Set starting settings
    tstorm.has_holo = -1; // last thunder sound
    tstorm.has_sensor = -1; // 2nd last thunder sound
    tstorm.has_tesla = 1; // Storm always start far away

    bprint(2,"Environmentç Lightning storm near the battlefield!\n");
};

//=======================================================
// Main think for storm entity

void() Storm_think =
{
    local float thunderdelay;

    if (self.has_sentry < 2)
    {
        thunderdelay = GetThunderDelay(self);

        StormLightning(self, thunderdelay);
    }
    else
    {
        self.nextthink = time + 1 + random() * 2;
        return;
    }
    
    self.nextthink = time + 5 + (20 - 5) * (1- self.has_tesla) + random()*random()*10;
};

//===========================================================================
// Gets the delay for a thunder in the storm

float(entity tstorm) GetThunderDelay =
{
    return 0 + (4 - 0) * (1- tstorm.has_tesla);
};

//====================================================================
// Cleans up everything related to the storm and ends it

void(float report) CleanUpStorm =
{
    local entity tstorm, te;
    
    // remove any storm events out there
    te = find(world, classname, "stormevent");

    while (te)
    {
        // remove this one..
        dremove(te);

        // ..and find the next one (if there is one)
        te = find(world, classname, "stormevent");
    }

    // find main storm entity..
    tstorm = find(world, classname, "storm");

    // ..and remove it
    if (tstorm) dremove(tstorm);

    // Reset world brighness to current daylight if needed after any storm lightning
    if (!ceasefire && !specialstate)
        SetWorldBrightness(daylight);

    if (report)
        bprint(2,"Environmentç The lighning storm has gone far away.\n");
};

//==============================================================
// Select and play an appropiate thunder sound from the storm

void(entity tstorm) StormThunder =
{
    if (ceasefire)
        return;
    
    local float thundernum;
    local float th1, th2, th3, counter, done, hs_sensor;

    if (tstorm.has_holo == -1 && tstorm.has_sensor == -1)
    {
        thundernum = rint(random()*3);
    }
    else 
    {
        th1 = 40;
        th2 = 40;
        th3 = 40;

        if (random() < 0.5 || tstorm.has_sensor < 0)
            hs_sensor = tstorm.has_sensor;
        else
            hs_sensor = 40;

        counter = 0;
        done = 0;

        while (counter < 4 && done == 0)
        {
            if (counter != tstorm.has_holo && counter != hs_sensor)
            {
                done = 1;
                th1 = counter;
            }

            counter = counter + 1;
        }

        counter = 0;
        done = 0;

        while (counter < 4 && done == 0)
        {
            if (counter != tstorm.has_holo && counter != hs_sensor && counter != th1)
            {
                done = 1;
                th2 = counter;
            }

            counter = counter + 1;
        }

        // third loop if we double use the 2nd last sound
        if (hs_sensor == 40)
        {
            counter = 0;
            done = 0;

            while (counter < 4 && done == 0)
            {
                if (counter != tstorm.has_holo && counter != hs_sensor && counter != th1 && counter != th2)
                {
                    done = 1;
                    th3 = counter;
                }

                counter = counter + 1;
            }

            counter = rint(random()*2);

            if (counter == 0)
                thundernum = th1;
            else if (counter == 1)
                thundernum = th2;
            else
                thundernum = th3;
        }
        else
        {
            counter = rint(random());

            if (counter == 0)
                thundernum = th1;
            else
                thundernum = th2;            
        }
    }
    
    // Get volume of thunder based on storm distance to battle field :)
    counter = 0.75 + (1 - 0.75) * tstorm.has_tesla;

    // Make thunder sound!
    ThunderSound(thundernum,counter);

    // Update last used thunder sounds
    tstorm.has_sensor = tstorm.has_holo;
    tstorm.has_holo = thundernum;
};

//===========================================================================================
// After lightning this is used by a storm event entity to play thunder and do clean up

void() Thunder_think =
{
    StormThunder(self.demon_one);

    if (self.has_teleporter || self.has_fieldgen != 6) // If not a double thing or 1 thunder of 2 already done..
    {
        self.demon_one.has_sentry = self.demon_one.has_sentry - 1;
        dremove(self);
    }
    else // this always get executed by a "double type" storm event
    {
        self.has_fieldgen = 1; // in next think, do the thing above
        self.nextthink = time + GetThunderDelay(self.demon_one);
    }
};

//=======================================================================
// Subroutine used for any lightning bright adjustments

void(float bright, entity stormevent) SetLightningBrightness =
{
    if (ceasefire)
        return;

    bright = bright - (bright*0.5)*(1-stormevent.demon_one.has_tesla);

    SetWorldBrightness(daylight + bright);
};

//========================================================================================
// Think for storm event entity, does lightning and passes to thunder_think when ended

void() StormEvent_think =
{    
    if (self.has_camera)
    {
        SetLightningBrightness(0,self);

        if (self.has_teleporter && self.has_fieldgen != 6) // Sound thunder played? then just do cleanup
        {
            self.demon_one.has_sentry = self.demon_one.has_sentry - 1;
            dremove(self);
            return;
        }
        else // we must play the thunder still
        {            
            self.think = Thunder_think;

            if (self.has_fieldgen == 6 && self.has_teleporter) // Double lightning/thunder event type
                self.nextthink = time + GetThunderDelay(self.demon_one);
            else
                self.nextthink = self.penance_time + self.job_finished;

            return;
        }
    }
    
    if (self.has_fieldgen == 0) // first type of lightning
    {
        if (self.heat == 0)
            SetLightningBrightness(15,self);
        else if (self.heat == 1)
            SetLightningBrightness(0,self);
        else if (self.heat == 2)
        {
            SetLightningBrightness(8,self);
            self.has_camera = 1;
        }
    }

    else if (self.has_fieldgen == 1) // second type of lightning
    {
        if (self.heat == 0)
            SetLightningBrightness(15,self);
        else if (self.heat == 1)
            SetLightningBrightness(10,self);
        else if (self.heat == 2)
        {
            SetLightningBrightness(3,self);
            self.has_camera = 1;
        }
    }    

    else if (self.has_fieldgen == 2) // third type of lightning
    {
        if (self.heat == 0)
            SetLightningBrightness(8,self);
        else if (self.heat == 1)
        {
            SetLightningBrightness(15,self);
            self.has_camera = 1;
        }
    }    

    else if (self.has_fieldgen == 3) // 4th Only sound
    {
        self.has_camera = 1;
        self.nextthink = time;
    }

    else if (self.has_fieldgen == 4) // 5th type of thunder
    {
        if (self.heat == 0)
            SetLightningBrightness(5,self);
        if (self.heat == 1)
        {
            SetLightningBrightness(4,self);
            self.has_camera = 1;        
        }
    }

    else if (self.has_fieldgen == 5) // 6th Only lightning
    {
        if (self.heat == 0)
            SetLightningBrightness(3,self);
        else if (self.heat == 1)
        {
            SetLightningBrightness(0,self);
            self.demon_one.has_sentry = self.demon_one.has_sentry - 1;
            dremove(self);
            return;
        }
    }

    else if (self.has_fieldgen == 6) // 7th double lightning/thunder
    {
        if (self.heat == 0)
            SetLightningBrightness(10,self);
        else if (self.heat == 1)
            SetLightningBrightness(4,self);
        else if (self.heat == 2)
            SetLightningBrightness(0,self);
        else if (self.heat == 11)
            SetLightningBrightness(15,self);
        else if (self.heat == 12)
            SetLightningBrightness(8,self);
        else if (self.heat == 13)
        {
            SetLightningBrightness(12,self);
            self.has_camera = 1;
        }        
    }

    if (!self.has_teleporter)
    if (self.has_fieldgen != 5)
    if (time > self.penance_time + self.job_finished)
    {
        StormThunder(self.demon_one);
        self.has_teleporter = 1; // Sound job done        
    }

    self.heat = self.heat + 1;

    if (self.heat >= 40) // for safety, shouldnt happen
    {
        self.demon_one.has_sentry = self.demon_one.has_sentry - 1;
        dremove(self);
        return;
    }

    self.nextthink = time + 0.1;
};

//==============================================================
// Spawn storm lightning event, which will become a thunder also

void(entity tstorm, float thunderdelay) StormLightning =
{
    if (ceasefire)
        return;

    local entity sevent;

    // Increase our counter
    tstorm.has_sentry = tstorm.has_sentry + 1;

    // Create storm event
    sevent = spawn();
    sevent.classname = "stormevent";
    sevent.think = StormEvent_think;
    sevent.nextthink = time + 0.1;
    sevent.demon_one = tstorm;
    sevent.heat = 0;
    sevent.penance_time = time;
    sevent.job_finished = thunderdelay;

    sevent.has_camera = 0;
    sevent.has_teleporter = 0;

    sevent.has_fieldgen = rint(random()*6); // random type of event
};

//=========================================================
// Starts the specified thunder sound on all clients

void(float thundernum, float vol) ThunderSound =
{
    local string st, st2;
    local entity te;

    st = "playvol ";

    if (thundernum == 0)
        st = strcat(st,"ambience/thunder1.wav ");
    else if (thundernum == 1)
        st = strcat(st,"environ/thunder2.wav ");
    else if (thundernum == 2)
        st = strcat(st,"environ/thunder3.wav ");
    else if (thundernum == 3)
        st = strcat(st,"environ/thunder4.wav ");
    else if (thundernum == 4) // Special sound of killing lightning
        st = strcat(st,"environ/thundrk.wav ");
    else
    {
        RPrint("BUG: Wrong thunder number on ThunderSound()!\n");
        return;
    }

    st2 = ftos(vol);
    st = strcat(st,st2);
    st = strcat(st,"\n");
    
    // find clients and stuffcmd the playvol command
    te = find(world, classname, "player");

    while (te)
    {
        if (te.is_connected)
            stuffcmd(te,st);
        
        te = find(te, classname,"player");
    }

    // spectators too..
    te = find(world, classname, "spec");

    while (te)
    {
        if (te.is_connected)
            stuffcmd(te,st);
        
        te = find(te, classname,"spec");
    }
};

//=====================================================================
// Returns TRUE if the thing has sky above it

float(entity thing) IsKillableByStorm =
{
    if (!thing) return 0;

    if (!IsBuilding(thing) && thing.classname != "player")
        return 0;

    traceline(thing.origin,thing.origin + '0 0 4096',1,thing);

    if (pointcontents(trace_endpos) == -6)
        return 1;
  
    return 0;
};

//=====================================================================
// Sets/Updates vulnerabilities on given machine 

void(entity thing) UpdateVulnerability =
{
    if (!thing) return;

    if (!IsBuilding(thing)) return;
    
    if (IsKillableByStorm(thing))
        thing.n_s_m = "strmtrgt";
    else
        thing.n_s_m = "";

    thing.n_s_h = "eqtarget";

    thing.is_connected = 1;
};

//======================================================================
// Happening of a special storm event (aka killer lightning)

void() StormSpecial_think =
{    
    if (self.heat == 0)
    {
        SetLightningBrightness(8,self);
    } 
    else if (self.heat == 1)
    {
        SetLightningBrightness(15,self);
    }
    else if (self.heat == 2)
    {
        SetLightningBrightness(12,self);
    }
    else if (self.heat == 3)
    {
        SetLightningBrightness(8,self);
    }
    else if (self.heat == 4)
    {
        SetLightningBrightness(4,self);
    }
    else if (self.heat == 5)
    {
        SetLightningBrightness(0,self);
        dremove(self);
        return;
    }

    if (self.heat >= 6)
    {
        SetLightningBrightness(0,self);
        dremove(self);
        return;
    }

    self.heat = self.heat + 1;
    self.nextthink = time + 0.1;
};

float(entity tstorm) StormSpecialEvent =
{
    if (ceasefire || intermission_running)
        return 0;
    
    local entity thetarget;

    if (random() > 0.75) // sometimes players have priority over machines for getting crushed
    {
        thetarget = StormSelectPlayer(tstorm);

        if (!thetarget) 
            thetarget = StormSelectMachine(tstorm);
    }
    else // usual thing
    {
        thetarget = StormSelectMachine(tstorm);

        if (!thetarget) 
            thetarget = StormSelectPlayer(tstorm);
    }

    // if no target is avaliable just return with failure
    if (!thetarget)
        return 0; 

    // Do the lightning ray effect
    traceline(thetarget.origin,thetarget.origin + '0 0 8192',1,thetarget);
    
    WriteByte (4, 23);
	WriteByte (4, 9);
    WriteEntity (4, thetarget);
    WriteCoord (4, thetarget.origin_x);
    WriteCoord (4, thetarget.origin_y);
    WriteCoord (4, thetarget.origin_z);
    WriteCoord (4, trace_endpos_x);
    WriteCoord (4, trace_endpos_y);
    WriteCoord (4, trace_endpos_z);
    
    multicast (thetarget.origin, 1);
    

    // Make our special louder thunder sound instantly..
    ThunderSound(4,1);
    
    // Electric sound
    sound (thetarget, 1, "weapons/lhit.wav", 1, 1);
    
    // Damage it
    deathmsg = 73;
    TF_T_Damage(thetarget,thetarget,thetarget,800,0,8);

    // TODO: Blast lightning damage on nearby stuff

    // Create an entity to handle the lightning brightness changes
    thetarget = spawn();
    thetarget.classname = "stormevent";
    thetarget.think = StormSpecial_think;
    thetarget.nextthink = time + 0.1;
    thetarget.heat = 0;
    thetarget.demon_one = tstorm;
    // lit start of lightning effect:
    SetLightningBrightness(25,thetarget);

    // we did it, return success..
    return 1;
};

//=====================================================================================
// The following functions select a machine/player as a special storm event target

entity(entity tstorm) StormSelectMachine =
{
    local entity te;
    local float done;

    done = 0;

    // Find any machine flagged as storm target
    te = find(world,n_s_m,"strmtrgt");

    while (done != 1 && te != world)
    {
        if (te.health > 0)
        if (IsBuilding(te))
        if (IsKillableByStorm(te))
        {
            done = 1;
        }

        if (!done)
            te = find(te,n_s_m,"strmtrgt");
    }

    if (te != world && done == 1) // we have a machine to be crushed by the storm
        return te;
    else
        return world;
};

entity(entity tstorm) StormSelectPlayer =
{
    if (num_players < 1) return world;

    local entity te;
    local float done;
    local float counter, toskip;

    toskip = 0;

    // Find player able to be killed by the storm

    te = find(world,classname,"player");

    if (num_players > 1) // if more than 1 player, skip random number of players first..
    {        
        toskip = floor(random()*num_players);
        counter = 0;

        while (counter < toskip)
        {
            counter = counter + 1;
            te = find(te,classname,"player");
        }
    }

    done = 0;
    counter = 0;

    while (done != 1 && counter < (num_players+1))// && te != world)
    {
        if (te.is_connected)
        if (te.playerclass != 0) // skip observers
        if (te.health > 0)
        if (IsKillableByStorm(te))
        {
            done = 1;
        }

        if (!done)
        {
            counter = counter + 1;
            te = find(te,classname,"player");
        }
    }

    if (te != world && done == 1) // we have a player to be crushed by the storm
        return te;
    else
        return world;
};


//---------------------------//
// * Earthquake *            //
// Random trembling ground   //
//---------------------------//

void() Earthquake_think;
float(entity eqent) GetNextEarthquakeTime;
void(entity eqent) EarthquakeStart;
void(entity eqent) EarthquakeEnd;
void() EarthquakeApply_think;

//========================================================================
// Initializes eathquake and starts earthquake stuff

void() InitEarthquake =
{
    local entity teq;

    CleanUpEarthquake(0);

    if (!earthquake) return;

    // Create earthquake entity
    teq = spawn();
    teq.classname = "eq";
    teq.think = Earthquake_think;
    teq.nextthink = time + GetNextEarthquakeTime(teq);
    teq.has_holo = 0;

    bprint(2,"Environmentç The sismology in this zone seems unstable!\n");
};

//===============================================
// Main think of earthquake entity

void() Earthquake_think =
{
    EarthquakeStart(self);
};

//=================================================================
// Starts the earthquake

void(entity eqent) EarthquakeStart =
{
    if (ceasefire || specialstate)
    {
        eqent.think = Earthquake_think;
        eqent.nextthink = time + GetNextEarthquakeTime(eqent);    
    }
    
    local entity te;

    eqent.think = EarthquakeApply_think;
    eqent.nextthink = time + 0.1;
    eqent.has_holo = 1;

    // Start earthquake sound on players
    te = find(world,classname,"player");

    while (te)
    {
        if (te.is_connected)
            clientsound(te,7,"environ/rumble.wav",0.75,3);
        
        te = find(te,classname,"player");
    }

    // Start earthquake sound on spectators
    te = find(world,classname,"spec");

    while (te)
    {
        if (te.is_connected)
            clientsound(te,7,"environ/rumble.wav",0.75,3);
        
        te = find(te,classname,"spec");
    }

    // Initialize counter and time flag
    eqent.heat = 0;
    eqent.penance_time = time;
    eqent.job_finished = 15 + (30 - 15) * random();
};

//================================================================
// Does the player movement stuff during an earthquake

void() EarthquakeApply_think =
{
    if (ceasefire || specialstate)
    {
        EarthquakeEnd(self);
        return;
    }
    
    local entity te;
    local vector movevec;

    // shake all players in same direction
    movevec_x = crandom()* 120;
    movevec_y = crandom()* 120;
    
    te = find(world,classname,"player");

    while (te)
    {
        if (te.is_connected)
        if (te.health > 0) // Skip heads and corpses (not respawned)?
        if (!(te.done_custom & 4)) // skip ppl customizing
        if (te.playerclass != 0) // skip observers
        {
            if (te.flags & 512) // player on ground, time for moving!
            {
                te.velocity_x = te.velocity_x + movevec_x;
                te.velocity_y = te.velocity_y + movevec_y;                
            }
            else if (te.waterlevel > 1) // player on water, move but slightly less
            {
                te.velocity_x = te.velocity_x + movevec_x * 0.5;
                te.velocity_y = te.velocity_y + movevec_y * 0.5;                
            }
        }

        te = find(te,classname,"player");
    }

    // Damage susceptible machines accordingly
    local float damage;

    damage = rint(random()*10);

    if (damage > 5)
    {
        deathmsg = 72;

        te = find(world,n_s_h,"eqtarget");

        while (te)
        {
            if (te.health > 0)
            if (random()< 0.4)
            {
                //if (te.flags & #FL_ONGROUND) // machine on ground
                    TF_T_Damage(te,te,te,damage,0,0);

                if (random()< 0.1)
                    SpawnBlood(te.origin,20);
            }

            te = find(te,n_s_h,"eqtarget");
        }
    }

    // Bprint warning message
    if (self.heat == 5)
        bprint(2,"ú ‘ËÂ ÁÚÔıÓ‰ ÚıÌ‚ÏÂÛ° ≈·ÚÙËÒı·ÎÂ°° ú\n");

    // Increase counter
    self.heat = self.heat + 1;

    // Set nextthink time randomly or end earthquake if needed
    if (time > self.penance_time + self.job_finished)
        EarthquakeEnd(self);
    else    
        self.nextthink = time + 0.1 + (0.2 - 0.1)*random();
};

//=================================================================
// Ends current earthquake

void(entity eqent) EarthquakeEnd =
{
    local entity te;

    if (eqent.has_holo != 1) return;

    eqent.think = Earthquake_think;
    eqent.nextthink = time + GetNextEarthquakeTime(eqent);    

    // Shut down earthquake sound on players
    te = find(world,classname,"player");

    while (te)
    {
        if (te.is_connected)
            clientsound(te,7,"environ/rumblend.wav",0.75,3);
        
        te = find(te,classname,"player");
    }    

    // Shut down earthquake sound on spectators
    te = find(world,classname,"spec");

    while (te)
    {
        if (te.is_connected)
            clientsound(te,7,"environ/rumblend.wav",0.75,3);
        
        te = find(te,classname,"spec");
    }    

    eqent.has_holo = 0;
};

//================================================================
// Returns next earthquake time

float(entity eqent) GetNextEarthquakeTime =
{
    return 10 + (20 - 10) * random();
};

//====================================================================
// Cleans up everything related to the earthquakes and ends them

void(float report) CleanUpEarthquake =
{
    local entity teq;

    teq = find(world, classname, "eq");

    if (teq)
    {
        EarthquakeEnd(teq); // (checks if needed itself)
        dremove(teq);
    }

    if (report)
        bprint(2,"Environmentç The earthquakes have ceased.\n");
};

//--------------------------------//
// * DayTime *                    //
// Emulates real dinamic daylight //
//--------------------------------//

void() DayTime_think;

//========================================================================
// Initializes daytime stuff and starts "real" day time emulation

void() InitDayTime =
{
    local entity daytimer;

    CleanUpDayTime(0);

    if (!daytime) return;

    // Create daytime entity
    daytimer = spawn();
    daytimer.classname = "daytimer";
    daytimer.think = DayTime_think;
    daytimer.nextthink = time + 1;
    daytimer.ltime = time; // Time at which days started
    daytimer.count = daytime; // Number of days
    daytimer.heat = timelimit / daytime; // single day size
    daytimer.has_tesla = 0; // first think will initialize this

    local string st;
    st = ftos(rint(daytime));

    bprint(2,"Environmentç This battle will last for ",st," days...\n");
};

//===================================================================
// Main think for day time emulation entity

void() DayTime_think =
{
    local float currentpos;

    if (specialstate || ceasefire || intermission_running)
    {
        self.nextthink = time + 1;
        return;
    }

    currentpos = (time - self.ltime)/ self.heat;

    daylight = getwave(currentpos,1|8,midnight_light,noon_light,0,0,0.6);

    daylight = rint(daylight);    
    
    SetWorldBrightness(daylight);

    if (daylight == noon_light)
    {
        if (self.has_tesla == 4)
            bprint(2,"ã Noon ã 13∫00 ã\n");
        
        self.has_tesla = self.has_tesla + 1;
    }
    else if (daylight == midnight_light)
    {
        if (self.has_tesla == 4)
            bprint(2," ÕÈ‰ÓÈÁËÙ  24∫00 \n");
        
        self.has_tesla = self.has_tesla + 1;
    }
    else 
        self.has_tesla = 0;
    
    self.nextthink = time + 1;
};

//====================================================================
// Cleans up everything related to the daytime emulation

void(float report) CleanUpDayTime =
{
    local entity daytimer;

    daytimer = find(world, classname, "daytimer");

    if (daytimer) dremove(daytimer);

    if (report)
        bprint(2,"Environmentç Day light is now static.\n");
};

