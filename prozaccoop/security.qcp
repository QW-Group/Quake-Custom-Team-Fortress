/*======================================================
	SECURITY.QC			Custom TeamFortress v3.1

	(c) Craig Hauser			26/3/00
========================================================
Functions for the security camera
======================================================*/
void() Security_Camera_Idle;
float() Security_Camera_FindTarget;
void(entity attacker, float damage) Security_Camera_Pain;
void() Security_Camera_Die;
void() Security_Camera_PrintTarget;
void() Security_Camera_Spawn;
float() Security_Camera_FindFakeTarget;
void() SecurityCameraTossTouch;

//used for print
string(float tno) GetTrueTeamName;
string(float pc) TeamFortress_GetClassName;
string(float pc) TeamFortress_GetJobName;

//===================================================================
void() Security_Camera_Idle =
{
	if (Security_Camera_FindTarget())
		self.nextthink = time + 2; //CH if found wait 2 sec before do another check
	else
		self.nextthink = time + 0.5; //CH else 2 checks per sec
	self.think = Security_Camera_Idle;
};
//========
float() Security_Camera_FindTarget =
{
	local entity client;
	local float	r, gotone, loopc;

	if (self.is_malfunctioning & 2) // SB how tragic, camera can't see
		return 0;
		
	if (self.is_malfunctioning & 4) // oh no, we're reading incorrect signals
	{
		if (Security_Camera_FindFakeTarget())
			return 1;
		else
			return 0;
	}

	// Try a few checks to make it react faster
	r = 0;
	loopc = 0;
	gotone = 0;

    local float trange; //- OfN - Hack
    trange=1000;

    if (self.all_active & 1)
        trange=1250;

	//CH Theortetically this will check every client on the server now
	while (loopc < 32 && gotone == 0)
	{
		client = checkclient();
		gotone = 1;

		if (!client)
			gotone = 0;

		if (!Pharse_Client(client, self, 1, trange, 0, 1))
			gotone = 0;

		loopc = loopc + 1;
		if (gotone) loopc = 1000;
	}

	if (!gotone)
		return 0;

	// Found a Target
	self.enemy = client;
	if (self.enemy.classname != "player")
	{
		self.enemy = self.enemy.enemy;
		if (self.enemy.classname != "player")
		{
			self.enemy = world;
			return 0;
		}
	}
	
	// Spotted sound
	if (self.heat == 3) { //CH so it does not beep all the time.
		sound (self, 1, "weapons/cambeep.wav", 1, 1);
		self.heat = 0;
	}
	else
		self.heat = self.heat + 1;

	Security_Camera_PrintTarget ();

	return 1;
};

float() Security_Camera_FindFakeTarget =
{
	local entity client;
	local float	r, gotone, loopc;

	if (self.is_malfunctioning & 4) // SB how tragic, camera can't see
		return 0;

	// Try a few checks to make it react faster
	r = 0;
	loopc = 0;
	gotone = 0;
	// Pick a random client
	while (loopc < 32 && gotone == 0)
	{
		client = checkclient();
		gotone = 1;

		if (!client)
			gotone = 0;

		if (random() < 0.05)
			gotone = 0;

		if (client.playerclass == 0) {
			return 0;
		}
		if (client.done_custom & 4) {
			return 0;
		}
		if (client.health <= 0) {
			return 0;
		}
		if (!client.is_connected) {
			return 0;
		}
		loopc = loopc + 1;
		if (gotone) loopc = 1000;
	}

	if (!gotone)
		return 0;

	// Found a Target
	self.enemy = client;
	if (self.enemy.classname != "player")
	{
		self.enemy = self.enemy.enemy;
		if (self.enemy.classname != "player")
		{
			self.enemy = world;
			return 0;
		}
	}
	
	// Spotted sound
	if (self.heat == 3) { //CH so it does not beep all the time.
		sound (self, 1, "weapons/cambeep.wav", 1, 1);
		self.heat = 0;
	}
	else
		self.heat = self.heat + 1;

	if (!(self.is_malfunctioning & 1))
		Security_Camera_PrintTarget ();

	return 1;
};
//========
void(entity attacker, float damage) Security_Camera_Pain =
{
	sound (self, 1, "weapons/cambeep.wav", 1, 1);
};
//======
void() Security_Camera_Die =
{
	sprint(self.real_owner, 2, "Your security camera was destroyed.\n");
	self.real_owner.has_camera = 0;

//	ThrowGib("progs/tgib1.mdl", -70);
//	ThrowGib("progs/tgib2.mdl", -70);
//	ThrowGib("progs/tgib3.mdl", -70);

	WriteByte (4, 23);
	WriteByte (4, 3);
	WriteCoord (4, self.origin_x);
	WriteCoord (4, self.origin_y);
	WriteCoord (4, self.origin_z);

	multicast (self.origin, 1);
    dremove(self);



    //SwitchFromCamera();
};
//==========
//Prints the person to owner/team
//Outputs: "<>SECURITY CAMERA<>  Team_color class/job (netname)\n"
void() Security_Camera_PrintTarget =
{
	local string st;

	sprint(self.real_owner, 2, "сецуриты цамера  "); //<>Security camera<>
	st = GetTrueTeamName(self.enemy.team_no);
	sprint(self.real_owner, 2, st);

	sprint(self.real_owner, 2, " ");
			if (self.enemy.playerclass != 11)
				st = TeamFortress_GetClassName(self.enemy.playerclass);
			else 
				st = TeamFortress_GetJobName(self.enemy.job);
	sprint(self.real_owner, 2, st);
	sprint(self.real_owner, 2, " (");
	sprint(self.real_owner, 2, self.enemy.netname);
	sprint(self.real_owner, 2, ")\n");
};
//==========
void() Security_Camera_Spawn =
{
	if (self.ammo_cells < 55)
	{
		sprint(self,2,"Not enough cells to build a Security Camera\n");		
	}
	else
	{
	self.ammo_cells = self.ammo_cells - 55;
	self.has_camera = 1;
	newmis = spawn();
	newmis.movetype = 10;
	setsize (newmis, '0 0 0', '0 0 0');
//	setsize (newmis, '-8 -8 -8', '8 8 8');
	newmis.solid = 2;
	newmis.takedamage = 2;
	newmis.classname = "building_camera";
	newmis.netname = "security_camera";
	setorigin (newmis, self.origin);
	newmis.owner = world;
	newmis.real_owner = self;
	makevectors (self.v_angle);
	newmis.avelocity = '0 0 0';
	newmis.velocity = v_forward*800 + v_up * 200 + v_right*10 + v_up*10;
	newmis.angles = '0 0 0';
	newmis.angles_y = anglemod(self.angles_y + 180);
//	newmis.skin = 1;
	newmis.th_die = Security_Camera_Die; 		// Death function
	newmis.th_pain = Security_Camera_Pain;
	newmis.mdl = "progs/camera.mdl"; //CH temp model
	setmodel (newmis, newmis.mdl);
	newmis.team_no = self.team_no;
	newmis.colormap = self.colormap;
	newmis.heat = 0; //Beeps

	newmis.health = newmis.max_health = 400;
	newmis.touch = SecurityCameraTossTouch;

    newmis.all_active=0; // OfN - reset HACKER improvements

	W_SetCurrentAmmo();
	}
};
//========
void() SecurityCameraTossTouch = 
{
	if (other != world || other == self.real_owner)
		return;
	if (pointcontents(self.origin) == -6 || pointcontents(self.origin + '0 0 2') == -6 || pointcontents(self.origin) == -2)
	{
		Security_Camera_Die();
		return;
	}
	//CH sees where landed and adjusts to proper things
	if (pointcontents(self.origin + '0 0 1') == -2)
		self.origin = self.origin - '0 0 12';
	if (pointcontents(self.origin - '0 0 1') == -2)
		self.origin = self.origin + '0 0 4';
	if (pointcontents(self.origin + '0 1 0') == -2)
		self.origin = self.origin - '0 16 0';
	if (pointcontents(self.origin - '0 1 0') == -2)
		self.origin = self.origin + '0 16 0';
	if (pointcontents(self.origin + '1 0 0') == -2)
		self.origin = self.origin - '16 0 0';
	if (pointcontents(self.origin + '1 0 0') == -2)
		self.origin = self.origin + '16 0 0';
	setorigin (self, self.origin);
	if (pointcontents(self.origin) == -6 || pointcontents(self.origin + '0 0 2') == -6 || pointcontents(self.origin) == -2)
	{
		Security_Camera_Die();
		return;
	}
	sprint (self.real_owner, 2, "You finish building the Security Camera.\n");

    teamprefixsprint(self.real_owner.team_no,self.real_owner); //- OfN
    teamsprint(self.real_owner.team_no, self.real_owner, self.real_owner.netname);
	teamsprint(self.real_owner.team_no, self.real_owner, " has built a Security Camera.\n");

    self.real_owner.option = time + 2; // so ppl cant destroy it for 2 seconds

	self.movetype = 0;
	setsize (self, '-16 -16 -6', '16 16 10');
	self.solid = 2;
	self.takedamage = 2;
	sound (self, 1, "weapons/guerset.wav", 1, 1);
	self.think = Security_Camera_Idle;
	self.nextthink = time + 1;
};

